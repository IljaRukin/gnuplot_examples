set parametric

#constants
N=10000
i={0.0,1.0}
rg=3
e=2.7182818284590452353602874

#functions
f(t)=e**(-1*i*t)
#g(t)=1/(4+2*sqrt(2)*(cos(t)+sin(t)))*e**(-1*i*t)
g(t)=1/(sqrt(2)*e**(i*t)+1+i)
#h(t)=1/(sqrt(2)*cos(t)+1+i*(sqrt(2)*sin(t)+1))
#h(t)=1/((sqrt(2)*cos(t)+1)**2+(sqrt(2)*sin(t)+1)**2)*e**(i*t+pi)


###plot range
set trange [0:2*pi]
set xrange [-rg:rg]
set yrange [-rg:rg]

###subdivions/details
set samples N
set isosamples N

###x/y axis at y=0
set ticslevel 0

###diargam apperance
#unset key
unset border

###dotted axis line at x/y=0
set xzeroaxis
set yzeroaxis

###axis label position, tics size
set xtics axis out scale 1,8 mirror norotate  autojustify
set ytics axis in scale 1,0.5 mirror norotate  autojustify

###labels
set title "Plot von w" font "sans, 20" 
set xlabel "reele Achse" offset 20,10,0
set ylabel "imaginäre Achse" offset 20,5,0

###equal axis lenght
set size square

###dot & line apperance
###line style number 1
set style line 1 linecolor rgb '#0060ad' linetype 1 linewidth 2 pointtype 7 pointsize 1.5 pi -1
###line style number 2
set style line 2 linecolor rgb '#dd0016' linetype 1 linewidth 2 pointtype 8 pointsize 1.5 pi -1
set style line 3 linecolor rgb '#00cc00' linetype 1 linewidth 2 pointtype 8 pointsize 1.5 pi -1

plot real(f(t)),imag(f(t)) title "w_1" with lines ls 1, \
real(g(t)), imag(g(t)) title "w_2" with lines ls 2